#!/usr/bin/env fish
# .Brewfile hash: {{ include (joinPath .chezmoi.homeDir "/.Brewfile") | sha256sum }}

printf "\e[1;33m[Darwin] Application tagging\e[0m\n"

# run only on darwin
string match -q "$CHEZMOI_OS" "darwin"; or exit 0

if string match -q (uname -m | string collect -a) "arm64"
  alias brew="arch -arm64 {{ .brew.prefix }}/bin/brew"
else
  alias brew="{{ .brew.prefix }}/bin/brew"
end

# cspell:disable-next-line
eval (brew shellenv)

## Ask for the administrator password upfront
sudo -v

## Keep-alive: update existing `sudo` time stamp until the parent has finished
# while true; sudo -n true; sleep 60; kill -0 "$fish_pid"; or exit; end 2>/dev/null &

printf "\e[1;34mtagging cask bundles...\e[0m\n"
brew info --cask --json=v2 (brew list --cask -1) | jq -r '.casks[].artifacts[].app // [] | .[] | .target? // .' | while read -l entry
  set -l artifact (string split -r -m 1 -f 2 "/" $entry)
  test -n "$artifact"; or set -l artifact $entry

  test -d "$HOME/Applications/$artifact"; and begin
    tag -s 'source:Brew' "$HOME/Applications/$artifact"
    continue
  end

  test -d "/Applications/$artifact"; and begin
    sudo tag -s 'source:Brew' "/Applications/$artifact"
    continue
  end
end

printf "\e[1;34mtagging Store bundles...\e[0m\n"
mas list | sed -E -e 's/[ ]+/ /g' -e 's/^[0-9]+ /\/Applications\//g' -e 's/ \([0-9.]+\)$/.app/g' | while read -l entry
  test -d "$entry"; and sudo tag -s 'source:Store' "$entry"
end

printf "\e[1;34mtagging package bundles...\e[0m\n"
brew info --cask --json=v2 (brew list --cask -1) | jq -r '[.casks[].artifacts[]?.uninstall[]?.pkgutil] | flatten | .[]' | sort -u | grep -v null | while read -l package
  # cspell:disable-next-line
  pkgutil --regexp --only-dirs --files $package | grep -iE '(.app|.prefpane)$' | while read -l entry
    test -d "$entry"; and sudo tag -s 'source:Brew' "/$entry"
  end
end

true
