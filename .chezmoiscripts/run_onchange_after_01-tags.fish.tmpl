#!/bin/fish
# .Brewfile hash: {{ include (joinPath .chezmoi.homeDir "/.Brewfile") | sha256sum }}

if string match -q (uname -m | string collect -a) "arm64"
  # cspell:disable-next-line
  eval "$(/opt/homebrew/bin/brew shellenv)"
  alias brew="arch -arm64 /opt/homebrew/bin/brew"
else
  # cspell:disable-next-line
  eval "$(/usr/local/Homebrew/bin/brew shellenv)"
  alias brew="/usr/local/Homebrew/bin/brew"
end

printf "\e[1;33mApplication tagging\e[0m\n"

## Ask for the administrator password upfront
sudo -v

## Keep-alive: update existing `sudo` time stamp until the parent has finished
# while true; sudo -n true; sleep 60; kill -0 "$fish_pid"; or exit; end 2>/dev/null &

echo "tagging cask bundles..."
brew info --cask --json=v2 (brew list --cask -1) | jq -r '.casks[].artifacts[].app // [] | .[] | .target? // .' | while read -l entry
  set -l artifact (string split -r -m 1 -f 2 "/" $entry)
  test -n "$artifact"; or set -l artifact $entry

  test -d "$HOME/Applications/$artifact"; and begin
    tag -s 'source:Brew' "$HOME/Applications/$artifact"
    continue
  end

  test -d "/Applications/$artifact"; and begin
    sudo tag -s 'source:Brew' "/Applications/$artifact"
    continue
  end

  echo "~404~ $artifact"
end

echo "tagging Store bundles..."
mas list | sed -E -e 's/[ ]+/ /g' -e 's/^[0-9]+ /\/Applications\//g' -e 's/ \([0-9.]+\)$/.app/g' | while read -l entry
  sudo tag -s 'source:Store' "$entry"
  or echo "failed to tag Store bundle $entry"
end

echo "tagging package bundles..."
brew info --cask --json=v2 (brew list --cask -1) | jq -r '[.casks[].artifacts[]?.uninstall[]?.pkgutil] | flatten | .[]' | sort -u | grep -v null | while read -l package
  # cspell:disable-next-line
  pkgutil --regexp --only-dirs --files $package | grep -iE '(.app|.prefpane)$' | while read -l entry
    sudo tag -s 'source:Brew' "/$entry"
    or echo "failed to tag package bundle $entry"
  end
end
